scriptencoding utf8

" --- Configuration Menu {{{
"--------------------------
"  [ Configuration Menu ]
"--------------------------
"
" -- About
" -- Start-up Process
" -- Plugins
"    : Thanks for creators;)
"    -- Shougo/neobundle.vim
"    -- yukpiz/vim-railscasts-theme
"    -- yukpiz/sakuramochi.vim
"    -- yukpiz/microsoft-translate.vim
"    -- thinca/vim-prettyprint
"    -- thinca/vim-ambicmd
"    -- kana/vim-submode
"    -- osyo-manga/vim-gift
"    -- Shougo/vimproc.vim
"    -- itchyny/calendar.vim
"    -- Shougo/vimshell.vim
"    -- yukpiz/nerdtree
"    -- haya14busa/incsearch.vim
"    -- vim-jp/vital.vim
"    -- Shougo/unite.vim
"    -- Shougo/unite-outline
"    -- tyru/open-browser.vim
"    -- tyru/caw.vim
"    -- osyo-manga/vim-brightest
"    -- nathanaelkane/vim-indent-guides
"    -- miyakogi/conoline.vim
"    -- rhysd/committia.vim
"    -- Shougo/neocomplete.vim
"    -- basyura/twibill.vim
"    -- basyura/TweetVim
"    -- yukpiz/redmine.vim
"    -- lambdalisue/vim-gista
"    -- kannokanno/previm
"    -- kchmck/vim-coffee-script
"    -- derekwyatt/vim-scala
"    -- dag/vim2hs
"    -- vim-jp/cpp-vim
"    -- vim-jp/vim-go-extra
"    -- yukpiz/vim-vbnet
"    -- hynek/vim-python-pep8-indent
"    -- thinca/vim-quickrun
"    -- Shougo/neosnippet
"    -- Shougo/neosnippet-snippets
"    -- plasticboy/vim-markdown
" -- Set Options
"    -- Encodings
"    -- Edit Behavior
"    -- Key Stroke
"    -- Indents
"    -- Text Search
"    -- Syntax & Styles
"    -- Fold
"    -- Register
"    -- Command Mode
"    -- Insert Mode
"    -- User Interface
"    -- Cursor
"    -- Tuning
" -- File Types
" -- Functions
" -- Key Mappings
"    -- Cheet Sheet
"    -- Cursor
"    -- Highlight
"    -- Shortcut
" }}}

" --- About {{{
"------------------------------------------
" [ About of this config ]
"------------------------------------------
" Configuration of Vim !!
"
" -- Repository
"
"      https://github.com/yukpiz/dotfiles.git
"
" -- Contact
"
"      Twitter: @yukpiz
"      Email:   yukpiz@gmail.com
" }}}
" --- Start-up Process {{{

let g:is_windows = has('win32')
let g:is_cygwin = has('win32unix')
let g:is_kaoriya = has('kaoriya')
let g:is_doswin = g:is_windows && !g:is_cygwin && !has('gui')
let g:is_unix = has('unix')
let g:is_mac = has('mac')
let g:is_macosx = has('macunix')

if g:is_windows && @% ==# ''
    execute 'cd $USERPROFILE'
endif

" }}}
" --- Plugins {{{

filetype off
filetype plugin indent off

" --- Shougo/neobundle.vim {{{
"if has('vim_starting')
    "Initialize NeoBundle.
    set runtimepath+=~/.vim/bundle/neobundle.vim
    call neobundle#begin(expand('~/.vim/bundle/'))
    NeoBundleFetch 'Shougo/neobundle.vim'
"endif
" }}}

" --- yukpiz/vim-railscasts-theme {{{
"NeoBundle 'yukpiz/vim-railscasts-theme'
" }}}

" --- yukpiz/sakuramochi.vim {{{
NeoBundle 'yukpiz/sakuramochi.vim'
" }}}

" --- yukpiz/microsoft-translate.vim {{{
"NeoBundle 'yukpiz/microsoft-translate.vim'
" }}}

" --- thinca/vim-prettyprint {{{
"vim-prettyprint to conveniently debug print,
" be printed shaping the dictionary of VimScript.
"NeoBundle 'thinca/vim-prettyprint'
" }}}

" --- thinca/vim-ambicmd {{{
" vim-ambicmd can predict and complement, to many plug-in commands.
" :nbi<Space> and typing, and complemented by camel case,
" will be output as :NeoBundleInstall
NeoBundle 'thinca/vim-ambicmd'
cnoremap <expr> <Space> ambicmd#expand('<Space>')
" }}}

" --- kana/vim-submode {{{
NeoBundle 'kana/vim-submode'

" }}}

" --- osyo-manga/vim-gift {{{
"NeoBundle 'osyo-manga/vim-gift'
" }}}

" --- Shougo/vimproc.vim {{{
NeoBundle 'Shougo/vimproc.vim', {
\  'build': {
\    'linux': 'make',
\  },
\}
" }}}

" --- itchyny/calendar.vim {{{
" Display a very beautiful calendar on the Vim.
" Cooperation also possible, with Google calendar.
"NeoBundle 'itchyny/calendar.vim'
"let g:calendar_frame = 'default'
"let g:calendar_google_calendar = 1
"let g:calendar_google_task = 1
" }}}

" --- Shougo/vimshell.vim {{{
"NeoBundle 'Shougo/vimshell.vim'
" }}}

" --- yukpiz/nerdtree {{{
NeoBundle 'yukpiz/nerdtree'
let g:NERDTreeChDirMode = 2
let g:NERDTreeDirArrows = '+'
let g:NERDTreeWinSize = 35
" }}}

" --- haya14busa/incsearch.vim {{{
NeoBundle 'haya14busa/incsearch.vim'
map / <Plug>(incsearch-forward)
map ? <Plug>(incsearch-backward)
map g/ <Plug>(incsearch-stay)
" }}}

" --- vim-jp/vital.vim {{{
NeoBundle 'vim-jp/vital.vim'
" }}}

" --- Shougo/unite.vim {{{
NeoBundle 'Shougo/unite.vim'
let g:unite_enable_start_insert=0
" }}}

" --- Shougo/unite-outline {{{
NeoBundle 'Shougo/unite-outline'
nmap ,out :Unite outline -winheight=10<CR>
" }}}

" --- tyru/open-browser.vim {{{
NeoBundle 'tyru/open-browser.vim'
" }}}

" --- tyru/caw.vim {{{
NeoBundle 'tyru/caw.vim'
nmap \c <Plug>(caw:I:toggle)
vmap \c <Plug>(caw:I:toggle)

nmap \C <Plug>(caw:I:uncomment)
vmap \C <Plug>(caw:I:uncomment)
" }}}

" --- osyo-manga/vim-brightest {{{
"NeoBundle 'osyo-manga/vim-brightest'
"let g:brightest#highlight = {
"\  'group': 'BrightestUnderline'
"\}
"let g:brightest#enable_filetypes = {
"\  'markdown': 0
"\}
" }}}

" --- nathanaelkane/vim-indent-guides {{{
"NeoBundle 'nathanaelkane/vim-indent-guides'
"let g:indent_guides_auto_colors=0
"autocmd VimEnter,Colorscheme * :hi IndentGuidesOdd   ctermbg=236
"autocmd VimEnter,Colorscheme * :hi IndentGuidesEven  ctermbg=235
"let g:indent_guides_enable_on_vim_startup=1
"let g:indent_guides_guide_size=1
" }}}

" --- miyakogi/conoline.vim {{{
"NeoBundle 'miyakogi/conoline.vim'
"let g:conoline_auto_enable=1
"let g:conoline_color_normal_dark='ctermbg=236'
"let g:conoline_color_insert_dark='ctermbg=239'

" }}}

" --- rhysd/committia.vim {{{
NeoBundle 'rhysd/committia.vim'
let g:committia_hooks = {}
function! g:committia_hooks.edit_open(info)
    setl spell
    if a:info.vcs ==# 'git' && getline(1) ==# ''
        startinsert
    end
    imap <buffer><C-n> <Plug>(committia-scroll-diff-down-half)
    imap <buffer><C-p> <Plug>(committia-scroll-diff-up-half)
endfunction
" }}}

" --- Shougo/neocomplete.vim {{{
NeoBundle 'Shougo/neocomplete.vim'
let g:neocomplete#enable_at_startup=1
let g:neocomplete#enable_smart_case=1
let g:neocomplete#enable_ignore_case=1
let g:neocomplete#enable_camel_case=0
let g:neocomplete#max_list=14
let g:neocomplete#max_keyword_width=80
let g:neocomplete#min_keyword_length=2

if !exists('g:neocomplete#keyword_patterns')
    let g:neocomplete#keyword_patterns={}
    let g:neocomplete#keyword_patterns['default']='\h\w'
endif

if !exists('g:neocomplete#sources#omni#input_patterns')
    let g:neocomplete#sources#omni#input_patterns={}
    let g:neocomplete#sources#omni#input_patterns.perl='\h\w*->\h\w*\|\h\w*::'
endif

inoremap <silent> <TAB> <C-r>=<SID>my_cr_function()<CR>
function! s:my_cr_function()
    return neocomplete#close_popup() . "\<CR>"
endfunction

inoremap <expr><TAB>  pumvisible() ? "\<C-n>" : "\<TAB>"
inoremap <expr><C-h> neocomplete#smart_close_popup()."\<C-h>"
inoremap <expr><BS> neocomplete#smart_close_popup()."\<C-h>"
inoremap <expr><C-y>  neocomplete#close_popup()
inoremap <expr><C-e>  neocomplete#cancel_popup()

augroup NeoCompleteStyle
    autocmd FileType css setl omnifunc=csscomplete#CompleteCSS
    autocmd FileType html,markdown setl omnifunc=htmlcomplete#CompleteTags
    autocmd FileType javascript setl omnifunc=javascriptcomplete#CompleteJS
    autocmd FileType python setl omnifunc=pythoncomplete#Complete
    autocmd FileType xml setl omnifunc=xmlcomplete#CompleteTags
augroup END
set completeopt-=preview
" }}}

" --- basyura/twibill.vim {{{
"NeoBundle 'basyura/twibill.vim'
" }}}

" --- basyura/TweetVim {{{
"NeoBundle 'basyura/TweetVim'
" }}}

" --- yukpiz/redmine.vim {{{
"NeoBundle 'yukpiz/redmine.vim'
" }}}

" --- lambdalisue/vim-gista {{{
"NeoBundle 'lambdalisue/vim-gista'
"let g:gista#github_user = 'yukpiz'
"let g:gista#update_on_write = 1
" }}}

" --- kannokanno/previm {{{
NeoBundleLazy 'kannokanno/previm'
" }}}

" --- kchmck/vim-coffee-script {{{
NeoBundleLazy 'kchmck/vim-coffee-script'
" }}}

" --- derekwyatt/vim-scala {{{
NeoBundleLazy 'derekwyatt/vim-scala'
" }}}

" --- dag/vim2hs {{{
NeoBundleLazy 'dag/vim2hs'
" }}}

" --- vim-jp/cpp-vim {{{
NeoBundleLazy 'vim-jp/cpp-vim'
" }}}

" --- vim-jp/vim-go-extra {{{
NeoBundleLazy 'vim-jp/vim-go-extra'
" }}}

" --- yukpiz/vim-vbnet {{{
NeoBundleLazy 'yukpiz/vim-vbnet'
" }}}

" --- hynek/vim-python-pep8-indent {{{
NeoBundleLazy 'hynek/vim-python-pep8-indent'
" }}}

" --- thinca/vim-quickrun {{{
"NeoBundleLazy 'thinca/vim-quickrun'
"let g:quickrun_config = {}
"let g:quickrun_config.cpp = {
"\  'command' : 'g++',
"\  'cmdopt' : '-std=c++11',
"\}
"let g:quickrun_config.go = {
"\  'command': 'go',
"\}
" }}}

" --- Shougo/neosnippet {{{
"NeoBundle 'Shougo/neosnippet'
"imap <C-s>     <Plug>(neosnippet_expand_or_jump)
"smap <C-s>     <Plug>(neosnippet_expand_or_jump)
"xmap <C-s>     <Plug>(neosnippet_expand_target)
"imap <expr><TAB> neosnippet#expandable_or_jumpable() ?
"\  "\<Plug>(neosnippet_expand_or_jump)"
"\  : pumvisible() ? "\<C-n>" : "\<TAB>"
"smap <expr><TAB> neosnippet#expandable_or_jumpable() ?
"\  "\<Plug>(neosnippet_expand_or_jump)"
"\  : "\<TAB>"
"
"if has('conceal')
"    set conceallevel=2 concealcursor=i
"endif
"
"let vbnet_plugin = neobundle#get('vim-vbnet')
"if !empty(vbnet_plugin) && has_key(vbnet_plugin, 'path')
"    let g:neosnippet#enable_snipmate_compatibility = 1
"    let g:neosnippet#snippets_directory = ''
"    if g:is_windows
"        "TODO: Generic config path on Windows.
"        let g:neosnippet#snippets_directory = 'C:\Users\yukpiz\.vim\bundle\vim-vbnet\snippet'
"    else
"        let g:neosnippet#snippets_directory = vbnet_plugin['path'] . '/snippet'
"    endif
"endif

" }}}

" --- Shougo/neosnippet-snippets {{{
"NeoBundle 'Shougo/neosnippet-snippets'
" }}}

" --- plasticboy/vim-markdown {{{
NeoBundleLazy 'rcmdnk/vim-markdown'
let g:vim_markdown_folding_disabled = 1
" }}}

call neobundle#end()
"とりあえずcall系のプラグイン設定はここに退避
call submode#enter_with('winsize', 'n', '', '<C-w>>', '<C-w>>')
call submode#enter_with('winsize', 'n', '', '<C-w><', '<C-w><')
call submode#enter_with('winsize', 'n', '', '<C-w>+', '<C-w>+')
call submode#enter_with('winsize', 'n', '', '<C-w>-', '<C-w>-')
call submode#map('winsize', 'n', '', '>', '<C-w>>')
call submode#map('winsize', 'n', '', '<', '<C-w><')
call submode#map('winsize', 'n', '', '+', '<C-w>+')
call submode#map('winsize', 'n', '', '-', '<C-w>-')

filetype plugin indent on

" }}}
" --- Set Options {{{
"Setting in 'set' command to write here.

" --- Encodings {{{
"Used in Vim internal encoding,
" it buffer and register of text,
" and in expression internal text, etc.
set encoding=utf-8
set fileencoding=utf-8
set fileencodings=utf-8,sjis,ucs-bom,default,latin1
"Line break code to be used.
set fileformats=unix,dos,mac
" }}}

" --- Edit Behavior {{{
"When a file is changed, reread automatically.
set autoread

"Not create undofile.
set noundofile

"The combination of 'writebackup' and 'nobackup',
" and then delete the backup file when have successfully save.
set writebackup
set nobackup
" }}}

" --- Key Stroke {{{

"Enable the backspace to auto indent,
" line break and beginning of line.
set backspace=indent,eol,start

" }}}

" --- Indents {{{
"Enable the indentation setting by the modeline.
set modeline

"When exceeds the width, and to new line at blank,
" but when the value is 0, it is disabled.
set textwidth=0

"Replaces <Tab> to blank.
set expandtab

"When entered the <Tab>, replaces to four blank.
set tabstop=4

"The number of blank to be used during the automatic indentation.
set shiftwidth=0

"When entered the new line,
" do automatic indentation.
set autoindent

"Unknown...
set smartindent

" }}}

" --- Text Search {{{
"After search until the end line,
" to return to the top line.
set wrapscan

"When the search,
" it is not distinguish between upper case and lower case.
set ignorecase

"When entered at upper case,
" it is distinguish between upper case and lower case.
set smartcase

"Do the incremental search.
set incsearch

"Highlighting the search result.
set hlsearch

"When search, to disabled the IME mode.
set imsearch=0
" }}}

" --- Syntax & Styles {{{
set wrap
set ambiwidth=double
set showmatch
set matchtime=3
set list listchars=tab:>.
set list listchars+=trail:_
set display=uhex
if has('syntax')
    function! ActivateInvisibleIndicator()
        syntax match InvisibleJISX0208Space "　"
        \ display containedin=ALL
        highlight InvisibleJISX0208Space
        \ term=underline
        \ ctermbg=LightGray
        \ guibg=LightGray
    endfunction
    augroup Invisible
        autocmd! Invisible
        autocmd BufNew,BufRead * call ActivateInvisibleIndicator()
    augroup END

    execute 'set colorcolumn=' . join(range(81, 9999), ',')
    syntax on

    set t_Co=256
    colorscheme sakuramochi
endif

" }}}

" --- Fold {{{
set foldmethod=marker
set foldopen+=jump
set foldenable
" }}}

" --- Register {{{
"Use a register of clipboard.
if has('xterm_clipboard')
    set clipboard+=autoselect
    set clipboard+=unnamed
    set clipboard+=unnamedplus
endif
" }}}

" --- Command Mode {{{
"Use a completion of the command mode.
if has('wildmenu')
    set wildmenu
    set wildchar=<Tab>
    set wildmode=full
endif

"The number of command history.
set history=50
" }}}

" --- Insert Mode {{{
"When start the insert mode, to disabled the IME mode.
set iminsert=0
" }}}

" --- User Interface {{{
"Text of titlebar of GUI Window.
set title
set titlestring=Hello\ VIM٩(ˊᗜˋ*)

"Show ruler at status line.
set ruler

"Not show line number.
set nonumber
set numberwidth=6

set laststatus=2
set statusline=%!GetStatusLineLabel()
"augroup CurrentWindowCursorLine
"    autocmd! CurrentWindowCursorLine
"    autocmd WinLeave,BufLeave * set nocursorline
"    autocmd WinEnter,BufRead * set cursorline
"augroup END
set guifont=Ricty\ 9
set guitablabel=%N:\ %{GetGuiTabLabel()}
set guioptions-=m
set guioptions-=T
" }}}

" --- Cursor {{{
"Buffer of area of scrolling.
set scrolloff=5
" }}}
" --- Tuning {{{
set lazyredraw
" }}}

" }}}
" --- File Types {{{
augroup CsharpConfigurations
    autocmd! CsharpConfigurations
    autocmd FileType cs call s:CsharpConfigurations()
augroup END

augroup JavaConfigurations
    autocmd! JavaConfigurations
    autocmd FileType java call s:JavaConfigurations()
augroup END

augroup XmlConfigurations
    autocmd! XmlConfigurations
    autocmd FileType xml call s:XmlConfigurations()
augroup END

augroup GoLangConfigurations
    autocmd! GoLangConfigurations
    autocmd FileType go call s:GoLangConfigurations()
augroup END

augroup CppConfigurations
    autocmd! CppConfigurations
    autocmd FileType cpp,c call s:CppConfigurations()
augroup END

augroup FileTypeCustomize
    autocmd! FileTypeCustomize
    autocmd FileType * set formatoptions=""
    autocmd BufNewFile,BufRead *.coffee set filetype=coffee
    autocmd BufNewFile,BufRead *.{md,mdwn,mkd,mkdn,mark*} set filetype=markdown
    autocmd BufNewFile,BufRead *.vb set filetype=vbnet
    autocmd BufNewFile,BufRead *.scala set filetype=scala
    autocmd BufNewFile,BufRead *.gradle set filetype=groovy
    autocmd FileType xml,html,yaml,eruby,css,scss set sw=2 ts=2 et
    autocmd FileType rst set sw=3 ts=3 et
    autocmd FileType ruby,python,css,coffee,haskell set sw=4 ts=4 et
    autocmd FileType javascript,java,php,vb,sh,make set sw=4 ts=4 et
    autocmd FileType vbnet set foldmarker=#Region,#End\ Region
    autocmd FileType vim set foldmarker={{{,}}}
    autocmd FileType vim let g:vim_indent_cont = 0
    autocmd FileType python set nosi
    "autocmd FileType vbnet call VbNetTrigger()

    "Syntax highlight of the standard class in Java.
    let g:java_highlight_all = 1
augroup END

augroup EnablePlugins
    autocmd! EnablePlugins
    autocmd FileType markdown NeoBundleSource previm vim-markdown
    autocmd FileType coffee NeoBundleSource vim-coffee-script
    autocmd FileType scala NeoBundleSource vim-scala
    autocmd FileType haskell NeoBundleSource vim2hs
    autocmd FileType cpp NeoBundleSource cpp-vim
    autocmd FileType vbnet NeoBundleSource vim-vbnet
    autocmd FileType python NeoBundleSource vim-python-pep8-indent
augroup END

" }}}
" --- Functions {{{
function! GetSyntaxName()
    return synIDattr(synID(line('.'), col('.'), 0), 'name')
endfunction

function! s:CsharpConfigurations()
    setlocal tabstop=4
    setlocal shiftwidth=4
    setlocal noexpandtab

    nnoremap <buffer><silent> <Space>ii :execute "?^using"<CR> :noh<CR> o
endfunction

function! s:JavaConfigurations()
    nnoremap <buffer><silent> <Space>ii :execute "?^import"<CR> :noh<CR> o
    setlocal tabstop=2
    setlocal shiftwidth=2
endfunction

function! s:CppConfigurations()
    "Set the directory path of the included libraries.
    setlocal path=,/usr/include,/usr/local/include

    "Setting of indentation characters.
    setlocal tabstop=4
    setlocal shiftwidth=4
    setlocal noexpandtab

    "Add to parenthese pairs.
    setlocal matchpairs+=<:>

    "Jump the end of the #include line, and change to insert mode.
    nnoremap <buffer><silent> <Space>ii :execute "?".&include<CR> :noh<CR> o

    "Show included file list.
    nmap ,inc :Unite file_include -winheight=10<CR>

    "Enable the syntax color of the boost libraries.
    syntax match boost_pp /BOOST_PP_[A-z0-9_]*/
    highlight link boost_pp cppStatement
endfunction

function! s:XmlConfigurations()
    command! -nargs=0 Fmt :%s/></>\r</g | filetype indent on | setf xml | normal gg=G
endfunction

function! s:GoLangConfigurations()
    if empty(system('which go')) ||
    \  empty(system('which gofmt')) ||
    \  empty(system('which godoc'))
        echo 'go-lang of the environment can not be found...'
        return
    endif

    NeoBundleSource vim-go-extra
    NeoBundleSource vim-quickrun
    autocmd BufWritePre <buffer> Fmt
    setl completeopt=menu,preview
    setl ts=4 noet
endfunction

"function! VbNetTrigger()
"    autocmd BufWritePre <buffer> call VbNetUpper()
"    autocmd BufWritePre <buffer> call VbNetSpace()
"endfunction

"let s:prev_line = line('.')
"function! VbNetUpper()
"    if s:prev_line ==# line('.')
"        return
"    endif
"    let line = getline(s:prev_line)
"
"endfunction

"let text1 = 'cur  >=  1 cur  <=  1 cur  !=  1'
"let text2 = 'cur  &=  1 cur  *=  1 cur  /=  1'
"let text3 = 'cur  \=  1 cur  ^=  1 cur  +=  1 cur  -=  1'
"let text4 = 'cur  =  1 cur  <  1 cur  >  1 cur  &  1 cur  *  1'
"let text5 = 'cur  /  1 cur  \  1 cur  ^  1 cur  +  1 cur  -  1'
"let text1 = substitute(text1, '\v\s+([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', '\1', 'g')
"let text2 = substitute(text2, '\v\s+([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', '\1', 'g')
"let text3 = substitute(text3, '\v\s+([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', '\1', 'g')
"let text4 = substitute(text4, '\v\s+([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', '\1', 'g')
"let text5 = substitute(text5, '\v\s+([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', '\1', 'g')
"let text1 = substitute(text1, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)\s+', '\1', 'g')
"let text2 = substitute(text2, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)\s+', '\1', 'g')
"let text3 = substitute(text3, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)\s+', '\1', 'g')
"let text4 = substitute(text4, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)\s+', '\1', 'g')
"let text5 = substitute(text5, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)\s+', '\1', 'g')
"echo substitute(text1, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', ' \1 ', 'g')
"echo substitute(text2, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', ' \1 ', 'g')
"echo substitute(text3, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', ' \1 ', 'g')
"echo substitute(text4, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', ' \1 ', 'g')
"echo substitute(text5, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', ' \1 ', 'g')

"let text1 = '(1  ,  2  ,  3  ,  4  ,  5  ,  6  ,)'
"let text1 = substitute(text1, '\v\s+(\,)', '\1', 'g')
"let text1 = substitute(text1, '\v(\,)\s+', '\1', 'g')
"echo substitute(text1, '\v(\,)', '\1 ', 'g')

"function! VbNetSpace()
"    let i = 1
"    for line in getline(0, '$')
"        let line = substitute(line, '\v\s+([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', '\1', 'g')
"        let line = substitute(line, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)\s+', '\1', 'g')
"        let line = substitute(line, '\v([\>|\<|\!|\&|\*|\/|\\|\^|\+|\-|\=]\=*)', ' \1 ', 'g')
"        let line = substitute(line, '\v\s+(\,)', '\1', 'g')
"        let line = substitute(line, '\v(\,)\s+', '\1', 'g')
"        let line = substitute(line, '\v(\,)', '\1 ', 'g')
"
"        call setline(i, line)
"        let i = i + 1
"    endfor
"
"    "let line = substitute(line, '\v\s\+\([\<|\>|\!|\&|*|\/|\\|\^|+|-].=\{,1}\)', '\1', 'g')
"    "let line = substitute(line, '\v\([\<|\>|\!|\&|*|\/|\\|\^|+|-].=\{,1}\)\s\+', '\1', 'g')
"    "let line = substitute(line, '\v\([\<|\>|\!|\&|*|\/|\\|\^|+|-].=\{,1}\)', ' \1 ', 'g')
"    "let line = substitute(line, '\s\+\([\=|\&|\*|\/|\\|\^|\+|\-].\)', '\1', 'g')
"    "let line = substitute(line, '\([\=|\&|\*|\/|\\|\^|\+|\-].\)\s\+', '\1', 'g')
"    "let line = substitute(line, '\([\=|\&|\*|\/|\\|\^|\+|\-]\)', ' \1 ', 'g')
"endfunction

function! GetStatusLineLabel()
    return '[%F]%h%m%r ' .
    \ '%{"[".(&ft!=""?&ft:"")."]"}' .
    \ '%{"[".(&fenc!=""?&fenc:&enc)."]"}' .
    \ '%{"[".(&ff!=""?&ff:"")."]"}' .
    \ '%=' .
    \ '[%v, %l/%L] %P [%b:0x%B]'
endfunction

function! GetGuiTabLabel()
    let l:label = ''
    let l:bufnrlist = tabpagebuflist(v:lnum)
    let l:bufname = fnamemodify(bufname(l:bufnrlist[tabpagewinnr(v:lnum) - 1]), ':t')
    let l:label .= l:bufname ==# '' ? 'No title' : l:bufname
    let l:wincount = tabpagewinnr(v:lnum, '$')
    if l:wincount > 1
        let l:label .= '[' . l:wincount . ']'
    endif
    for bufnr in l:bufnrlist
        if getbufvar(bufnr, '&modified')
            let l:label .= '[+]'
            break
        endif
    endfor
    return l:label
endfunction

function! SwitchNumber()
    if &nu == 0
        set nu
    else
        set nonu
    endif
endfunction

let g:calendar_on = 0
function! SwitchCalendar()
    call s:all_close_calendar()
    if g:calendar_on == 0
        Calendar -view=month -split=horizontal -position=below -height=25
        let w:calendar_window = 1
        let g:calendar_on = 1
    else
        let g:calendar_on = 0
    endif
endfunction

function! s:all_close_calendar()
    let l:li = gift#tabpagewinnr_list()
    for tw in l:li
        call gift#jump_window(tw)
        if exists('w:calendar_window') == 1
            if w:calendar_window == 1
                call gift#close_window(tw)
                call s:all_close_calendar()
            endif
        endif
    endfor
endfunction

function! OpenBrowserParsedURL()
    let mlist = matchlist(getline('.'), '\v[(|\[|\s](https?:\/\/[^>]*)[)|\]|\s]')
    if mlist != []
        call openbrowser#open(mlist[1])
    else
        echo 'URL is not found in the current line.'
    endif
endfunction

" }}}
" --- Key Mappings {{{

" --- Cheet Sheet {{{
" # Key Mappings Cheet Sheet
" +---------------+--------+--------+---------+--------+
" | Command       | Normal | Insert | Command | Visual |
" |:------------- |:------:|:------:|:-------:|:------:|
" | map/noremap   | o      | x      | x       | o      |
" | nmap/nnoremap | o      | x      | x       | x      |
" | imap/inoremap | x      | o      | x       | x      |
" | cmap/cnoremap | x      | x      | o       | x      |
" | vmap/vnoremap | x      | x      | x       | o      |
" | map!/noremap! | x      | o      | o       | x      |
" +---------------+--------+--------+---------+--------+

"
" | Arguments | Description 
" |:--------- |:------------------
" | <buffer>  |
" | <nowait>  |
" | <silent>  |
" | <special> |
" | <script>  |
" | <expr>    |
" | <unique>  |
" }}}

" --- Cursor {{{
nnoremap <Up> <Nop>
nnoremap <Down> <Nop>
nnoremap <Left> <Nop>
nnoremap <Right> <Nop>
inoremap <Up> <Nop>
inoremap <Down> <Nop>
inoremap <Left> <Nop>
inoremap <Right> <Nop>
inoremap <C-k> <Up>
inoremap <C-j> <Down>
inoremap <C-h> <Left>
inoremap <C-l> <Right>
"nnoremap <C-f> <End>
"nnoremap <C-d> <Home>
"inoremap <C-d> <Home>
"inoremap <C-f> <End>
nnoremap <C-o> :call OpenBrowserParsedURL()<CR>
inoremap <C-o> <ESC>:call OpenBrowserParsedURL()<CR>

nnoremap j gj
nnoremap k gk
vnoremap j gj
vnoremap k gk
" }}}

" --- Buffer {{{
nnoremap <silent> gp :bprevious<CR>
" }}}

" --- Highlight {{{
nnoremap <silent> <Esc><Esc> :noh<CR>
" }}}

" --- Shortcut {{{
if g:is_unix
    nnoremap ,vs :VimShell<CR>
endif
nnoremap ,cl :call SwitchCalendar()<CR>
nnoremap ,nt :NERDTreeToggle<CR>
nnoremap ,nu :call SwitchNumber()<CR>
nnoremap ,gi :Unite gista<CR>
" }}}

" }}}
